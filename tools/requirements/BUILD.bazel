load("@rules_req_compile//:defs.bzl", "py_reqs_compiler", "py_reqs_solution_test")

exports_files(glob(["*.txt"]))

PLATFORMS = [
    "linux-aarch64",
    "linux-x86_64",
    "macos-aarch64",
    "windows-x86_64",
]

filegroup(
    name = "requirements_in",
    srcs = [
        "dev_requirements.in",
    ],
    data = [
        "//jupyter:requirements.in",
    ],
)

[
    py_reqs_compiler(
        name = "requirements.{}.update".format(platform.replace("-", "_")),
        requirements_in = "requirements_in",
        requirements_txt = "requirements_{}.txt".format(platform.replace("-", "_")),
        target_compatible_with = select({
            "//tools/constraints:{}".format(platform): [],
            "//conditions:default": ["@platforms//:incompatible"],
        }),
    )
    for platform in PLATFORMS
]

[
    py_reqs_solution_test(
        name = "requirements_{}_test".format(platform.replace("-", "_")),
        compiler = ":requirements.{}.update".format(platform.replace("-", "_")),
    )
    for platform in PLATFORMS
]

alias(
    name = "update",
    actual = select({
        "//tools/constraints:{}".format(platform): ":requirements.{}.update".format(platform.replace("-", "_"))
        for platform in PLATFORMS
    }),
)
